

'...................Predictive Engineering, Inc....................................................
'
'Updated 2010
'
'Predictive Engineering Assumes No Responsibility For Results Obtained From API
'Written for FEMAP v10.1.1
'The time step generated by this program matches that calculated by LS-DYNA v971
'This API calculates the required timestep for the following element types:
'8-Node Brick, 6-Node Extruded Wedge, 4-Node Tet, 10-Node Tet, 3-Node Tri-Plate, 4-Node Quad Plate


'We are going to begin by declaring some public variables, these will be used by all parts of the program

Public elemNum(), elemLast, elemTotal, OutputNums(3), minelID As Long
Public globalMin As Double

'This is the main control program

Sub Main

	Dim App As femap.model
	Set App = feFemap()

	globalMin = 10e6

Call elemGetter

If elemTotal = 0 Then
	App.feAppMessageBox(0,"No Elements Selected, Program Terminating")
	GoTo FAIL
End If

Call Plates

Call Tets

Call Hexs

Call OutputCombiner

FAIL:

End Sub

'This script requests the user to specify which elements they are interested in,
'then it builds a array of the element numbers which will be used by each sub-program

Sub elemGetter()

	Dim femap As femap.model
	Set femap = GetObject(,"femap.model")

Dim elemSet As Object
Set elemSet = femap.feSet

rc = elemSet.Select(8, True, "Select Elements for Time-Step Calc")

elemTotal = elemSet.Count
ReDim elemNum(elemSet.Count)
Dim i As Long

elemLast = elemSet.Last
rc = elemSet.Reset

Do

elemNum(i) = elemSet.Next
i = i + 1

Loop Until elemNum(i-1) = elemLast

End Sub

'This script calculates the timestep for tri-3 & quad-4 plates

Sub Plates()
	Dim App As femap.model
	Set App = feFemap()
	Dim rc As Long

	App.feAppMessage(0,"Plate Routine Started")

	Dim elemSet As femap.Set
    Dim feElem As femap.Elem
    Dim elID As Long
    Dim feProp As femap.Prop
    Dim feMatl As femap.Matl
    Dim nodeX As Double
    Dim nodeY As Double
    Dim nodeZ As Double
    Dim nodeID As Long
    Dim ElemID As Long
    Dim feOutputSet As femap.OutputSet
    Dim osID As Long
    Dim feOutput As femap.Output
    Dim ovID As Long
    Dim textOut As String
    Dim c As Double
    Dim feNode1 As femap.Node
    Dim feNode2 As femap.Node
    Dim feNode3 As femap.Node
    Dim feNode4 As femap.Node
    Dim ts As Double
    Dim elArea As Double
    Dim eCount As Long
    Dim eIndex As Long

    'Mass Properties Stuff
    Dim mmp1 As Double
    Dim mmp2 As Double
    Dim mmp3 As Double
    Dim mmp4 As Double
    Dim mmp5 As Double

    Dim v1 As Variant
    Dim v2 As Variant
    Dim v3 As Variant
    Dim v4 As Variant
    Dim v5 As Variant

    Dim L1, L2, L3, L4, L, LS As Double

    Dim outputVector As Long

    Dim veID As Variant
    Dim veValue As Variant

    'Check for an Active Output Set

    Set feOutputSet = App.feOutputSet()
	Set feOutput = App.feOutput()
    Set feProp = App.feProp()
    Set feMatl = App.feMatl()
    Set feElem = App.feElem()
    Set feNode1 = App.feNode()
    Set feNode2 = App.feNode()
    Set feNode3 = App.feNode()
    Set feNode4 = App.feNode()
    Set elemSet = App.feSet()

        osID = feOutputSet.NextEmptyID()
        OutputNums(0) = osID
        feOutputSet.title = "Wave Speed"
        feOutputSet.program = 1
        feOutputSet.analysis = 0
        feOutputSet.Value = 0#
        feOutputSet.Put (osID)


    ' Now get an Output Vector

    feOutput.ID = 299999

    ovID = feOutput.NextEmptyID()
    feOutput.ID = ovID

    rc = feOutput.InitScalarAtElem(osID, ovID, "Wave Speed", 0, True)


        eCount = elemTotal

        ReDim elementID(eCount) As Long
        ReDim eValues(eCount) As Double
		Dim q As Long
        eIndex = -1

        rc = App.feAppStatusShow(True, eCount)

        Do
        	ElemID = elemNum(q)
            rc = feElem.Get(ElemID)
            'Make sure that it is a shell
            If feElem.type = 17 Then

                rc = feProp.Get(feElem.propID)
                rc = feMatl.Get(feProp.matlID)
                c = Sqr(feMatl.Ex / (feMatl.Density * (1 - feMatl.Nuxy * feMatl.Nuxy)))

                'c is calculated using LS-DYNA theory manual equation #22.14

                If feElem.topology = 2 Then 'Triangle

                    eIndex = eIndex + 1
                    ' Get the nodes
                    feNode1.Get (feElem.Node(0))
                    feNode2.Get (feElem.Node(1))
                    feNode3.Get (feElem.Node(2))
                    L1 = Distance(feNode2.x, feNode2.y, feNode2.z, feNode1.x, feNode1.y, feNode1.z)
                    L2 = Distance(feNode3.x, feNode3.y, feNode3.z, feNode2.x, feNode2.y, feNode2.z)
                    L3 = Distance(feNode3.x, feNode3.y, feNode3.z, feNode1.x, feNode1.y, feNode1.z)
                    L = FindMax(L1, L2, L3, 0)
                    rc = App.feMeasureMeshMassProp(-feElem.ID, 0, False, False, mmp1, elArea, mmp2, mmp3, mmp4, mmp5, v1, v2, v3, v4, v5)

                    LS = (2 * elArea) / L

                    'LS represents the Characteristic Length, the formula for its calculation is taken from
                    'the LS-DYNA theory manual equation #22.15

                    ts = LS / c

                    'ts represents the timestep, this value is arrived at using LS-DYNA thoery manual
                    'equation #22.13

                    If ts < globalMin Then
                        globalMin = ts
                        minelID = feElem.ID
                    End If

                    elementID(eIndex) = feElem.ID
                    eValues(eIndex) = ts

                End If

                If feElem.topology = 4 Then 'Quad
                    eIndex = eIndex + 1
                    feNode1.Get (feElem.Node(0))
                    feNode2.Get (feElem.Node(1))
                    feNode3.Get (feElem.Node(2))
                    feNode4.Get (feElem.Node(3))
                    L1 = Distance(feNode2.x, feNode2.y, feNode2.z, feNode1.x, feNode1.y, feNode1.z)
                    L2 = Distance(feNode3.x, feNode3.y, feNode3.z, feNode2.x, feNode2.y, feNode2.z)
                    L3 = Distance(feNode4.x, feNode4.y, feNode4.z, feNode3.x, feNode3.y, feNode3.z)
                    L4 = Distance(feNode1.x, feNode1.y, feNode1.z, feNode4.x, feNode4.y, feNode4.z)
                    L = FindMax(L1, L2, L3, L4)

                    rc = App.feMeasureMeshMassProp(-feElem.ID, 0, False, False, mmp1, elArea, mmp2, mmp3, mmp4, mmp5, v1, v2, v3, v4, v5)

                    LS = elArea / L

                    'LS represents the Characteristic Length, the formula for its calculation is taken from
                    'the LS-DYNA theory manual equation #22.15

                    ts =LS / c

                    'ts represents the timestep, this value is arrived at using LS-DYNA thoery manual
                    'equation #22.13

                    If ts < globalMin Then
                        globalMin = ts
                        minelID = feElem.ID
                    End If

                    elementID(eIndex) = feElem.ID
                    eValues(eIndex) = ts

                    rc = App.feAppStatusUpdate(eIndex)

                End If
            End If
            q = q + 1
        Loop Until elemNum(q-1) = elemLast

        rc = App.feAppStatusShow(False, 0)

        veID = elementID
        veValue = eValues

        rc = feOutput.PutOutputList(eCount, veID, veValue)

        rc = feOutput.Put(300000)

    End Sub


Function Distance(ByVal x1 As Double, ByVal y1 As Double, ByVal z1 As Double, ByVal x2 As Double, ByVal y2 As Double, ByVal z2 As Double) As Double

    Distance = Sqr((x2 - x1) * (x2 - x1) + (y2 - y1) * (y2 - y1) + (z2 - z1) * (z2 - z1))

End Function

Function FindMax(ByVal x1 As Double, ByVal x2 As Double, ByVal x3 As Double, ByVal x4 As Double) As Double

    Dim localMax As Double

    localMax = 0

    If x1 > localMax Then
        localMax = x1
    End If

    If x2 > localMax Then
        localMax = x2
    End If

    If x3 > localMax Then
        localMax = x3
    End If

    If x4 > localMax Then
        localMax = x4
    End If

    FindMax = localMax

End Function

'This script calculates the timestep for 4- & 10-node Tets

Sub Tets()

	Dim femap As femap.model
	Set femap = GetObject(,"femap.model")

	femap.feAppMessage(0,"Tet Routine Started")

	Dim el As Object
	Set el=femap.feElem

	Dim nd As Object
	Set nd = femap.feNode

	Dim mat As Object
	Set mat = femap.feMatl

	Dim proper As Object
	Set proper = femap.feProp

	Dim elemSet As Object
	Set elemSet = femap.feSet

	Dim feOutputSet As Object
	Set feOutputSet = femap.feOutputSet

	Dim feOutput As Object
	Set feOutput = femap.feOutput

	Dim e As Double
	Dim v As Double
	Dim rho As Double
	Dim wSpeed() As Double
	Dim matLast As Long
	Dim matID As Long
	Dim rc As Long
	Dim textOut As String


	rc = mat.Last()
	matLast = mat.ID
	ReDim wSpeed(matLast+1) As Double
	rc = mat.Reset

	If matLast = 0 Then
		femap.feAppMessageBox(0,"No Material Properties Have Been Specified")
		GoTo FAIL
	End If

		Do
			rc = mat.Next
			matID = mat.ID
			e = mat.mval(0)
			v = mat.mval(6)
			rho = mat.mval(49)
				If e = 0 Then
					GoTo SKIPMAT
				End If
				If v = 0 Then
					GoTo SKIPMAT
				End If
				If rho = 0 Then
					GoTo SKIPMAT
				End If
			wSpeed(matID) = (e*(1-v)/(rho*(1+v)*(1-2*v)))^(0.5)

			'wSpeed is the wave speed, and is equation #22.8 of the LS-DYNA theory manual

			SKIPMAT:
		Loop Until matID = matLast

	Dim goFlag As Boolean
	goFlag = False
	
	Dim elType As Long
	Dim elLast As Long
	Dim ElemID As Long
	'Dim valASPECT As Double
	'Dim valTAPER As Double
	'Dim valANGLE As Double
	'Dim valWARP As Double
	'Dim valALTTAPER As Double
	'Dim valTET As Double
	'Dim valJDet As Double

	Const maxEls = 100000

	Dim elLIST(maxEls) As Long
	Dim aspect(maxEls) As Double
	Dim angle(maxEls) As Double
	Dim Taper(maxEls) As Double
	Dim warp(maxEls) As Double
	Dim alttaper(maxEls) As Double
	Dim tet(maxEls) As Double
	Dim Jacob(maxEls) As Double
	Dim Comb(maxEls) As Double
	Dim NasWarp(maxEls) As Double

'------------------------------------new Code



	Dim eIndex As Long
	Dim Length(6) As Double
	Dim elNode(4) As Long
	Dim x(4) As Double
	Dim y(4) As Double
	Dim z(4) As Double
	Dim t As Long
	Dim maxLength As Double
	Dim charLength As Double
	Dim pID As Long
	Dim timeStep As Double
	Dim elCount As Long
	Dim velCount As Variant
	Dim elemOutIDS() As Variant
	Dim elemOutValues() As Variant
	Dim q As Long
	Dim SF As Double

	elCount = elemTotal

	velCount = elCount
	ReDim elemOutIDS(elCount) As Variant
	ReDim elemOutValues(elCount) As Variant
	t = 0
	eIndex = 0

	rc = femap.feAppStatusShow(True, elCount)

	Do
		ElemID = elemNum(q)
		rc = el.Get(ElemID)

		If (el.topology = 6) Or (el.topology = 10) Then

		'SF represents a scaling factor used to translate the timestep of a 4-node Tet,
		'to that of a 10-node Tet
		'Jim Day at LSTC wrote the following:
		'"Although it's unclear how the factor was derived,
		'ELFORM 16 has a 0.3889 scaling factor applied to its characteristic length and
		'thus to its time step (as compared to a scaling factor of 1.0 for 4-noded tet, ELFORM 4)."
		'So the script checks to see what order the Tet is, and applies the appropriate scaling factor

			If el.topology = 6 Then
				SF = 1
			ElseIf el.topology = 10 Then
				SF = 0.3889
			End If


		eIndex = eIndex + 1

		elNode(0) = el.Node(0)
		elNode(1) = el.Node(1)
		elNode(2) = el.Node(2)
		elNode(3) = el.Node(4)

			For i = 0 To 3
				rc = nd.Get(elNode(i))
				x(i) = nd.x
				y(i) = nd.y
				z(i) = nd.z
			Next i

		Length(0) = ((x(1)-x(0))^2+(y(1)-y(0))^2+(z(1)-z(0))^2)^(0.5)
		Length(1) = ((x(2)-x(0))^2+(y(2)-y(0))^2+(z(2)-z(0))^2)^(0.5)
		Length(2) = ((x(3)-x(0))^2+(y(3)-y(0))^2+(z(3)-z(0))^2)^(0.5)
		Length(3) = ((x(2)-x(1))^2+(y(2)-y(1))^2+(z(2)-z(1))^2)^(0.5)
		Length(4) = ((x(1)-x(3))^2+(y(1)-y(3))^2+(z(1)-z(3))^2)^(0.5)
		Length(5) = ((x(2)-x(3))^2+(y(2)-y(3))^2+(z(2)-z(3))^2)^(0.5)

		maxLength = Length(0)

			For i = 1 To 5
				If Length(i) > maxLength Then
					maxLength = Length(i)
				End If
			Next i

		'The following routine uses a distorion check to get the minimum altitude.  Minimum altitude of
		'a tet is what defines the characteristic length according to pg. 22.1 of the LS-DYNA theory manual

		'rc = femap.feGetElemDistortion( ElemID, valASPECT, valTAPER, valANGLE, valWARP, valALTTAPER, valTET, valJDet)

		rc = femap.feGetElemDistortion( ElemID, aspect(i), Taper(i), angle(i), warp(i), NasWarp(i),  alttaper(i), tet(i), Jacob(i), Comb(i) )

		charLength = SF*maxLength / tet(i)

		pID = el.propID
		rc = proper.Get(pID)
		matID = proper.matlID

		If wSpeed(matID) = 0 Then
			textOut = "Material ID "+Str$(matID)+" does not have the needed properties specified"
			femap.feAppMessageBox(0,textOut)
			GoTo FAIL
		End If

		timeStep = charLength/(0.06*wSpeed(matID)+((0.06*wSpeed(matID))^2+(wSpeed(matID))^2)^0.5)

		'This timestep calculation is taken from equation #22.2 of the LS-DYNA theory manual

		elemOutValues(t) = timeStep
		elemOutIDS(t) = ElemID
		t = t + 1

		If timeStep < globalMin Then
			globalMin = timeStep
			minelID = ElemID
		End If

		End If

		rc = femap.feAppStatusUpdate(q)
		q = q + 1

	Loop Until elemNum(q-1) = elemLast


'------------------------Output Section


Dim osID As Variant
Dim ovID As Long


'    If osID = 0 Then
		'No Output Set, need to make one
		osID = feOutputSet.NextEmptyID()
        OutputNums(1) = osID
        feOutputSet.title = "Wave Speed"
        feOutputSet.program = 1
        feOutputSet.analysis = 0
        feOutputSet.value = 0#
        feOutputSet.Put (osID)
'    End If

            OutputNums(1) = osID

    ' Now get an Output Vector

    feOutput.ID = 299999

    ovID = feOutput.NextEmptyID()
    feOutput.ID = ovID

    rc = feOutput.InitScalarAtElem(osID, ovID, "Wave Speed", 0, True)

   veID = elementID
   veValue = eValues
   rc = feOutput.PutOutputList(elCount, elemOutIDS, elemOutValues)

   rc = feOutput.Put(300000)


FAIL:


End Sub

Sub Hexs()
	Dim femap As femap.model
	Set femap = GetObject(,"femap.model")

	femap.feAppMessage(0,"Hex Routine Started")

	Dim el As Object
	Set el=femap.feElem

	Dim nd As Object
	Set nd = femap.feNode

	Dim mat As Object
	Set mat = femap.feMatl

	Dim proper As Object
	Set proper = femap.feProp

	Dim elemSet As Object
	Set elemSet = femap.feSet

	Dim feOutputSet As Object
	Set feOutputSet = femap.feOutputSet

	Dim feOutput As Object
	Set feOutput = femap.feOutput

	Dim e As Double
	Dim v As Double
	Dim rho As Double
	Dim wSpeed() As Double
	Dim matLast As Long
	Dim matID As Long
	Dim rc As Long
	Dim textOut As String
	Dim elemSetID As Long
	Dim SF As Double


	rc = mat.Last()
	matLast = mat.ID
	ReDim wSpeed(matLast+1) As Double
	rc = mat.Reset

	If matLast = 0 Then
		femap.feAppMessageBox(0,"No Material Properties Have Been Specified")
		GoTo FAIL
	End If

		Do
			rc = mat.Next
			matID = mat.ID
			e = mat.mval(0)
			v = mat.mval(6)
			rho = mat.mval(49)
				If e = 0 Then
					GoTo SKIPMAT
				End If
				If v = 0 Then
					GoTo SKIPMAT
				End If
				If rho = 0 Then
					GoTo SKIPMAT
				End If
			wSpeed(matID) = (e*(1-v)/(rho*(1+v)*(1-2*v)))^(0.5)

			'wSpeed is the wave speed, and is equation #22.8 of the LS-DYNA theory manual

			SKIPMAT:
		Loop Until matID = matLast


'------------------------------------new Code


Dim elCount As Long

elCount = elemTotal

Dim elemOutIDS() As Variant
Dim elemOutValues() As Variant

ReDim elemOutIDS(elCount)
ReDim elemOutValues(elCount)
Dim t As Long

Dim elCurr As Long
Dim dArea As Double
Dim dAreaHolder As Double
Dim charLength As Double
Dim pID As Long
Dim timeStep As Double

Dim Length As Double
Dim area As Double
Dim volume As Double
Dim structMass As Double
Dim nonstructMass As Double
Dim totalMass As Double
Dim structCG As Variant
Dim nonstructCG As Variant
Dim totalCG As Variant
Dim inertia As Variant
Dim inertiaCG As Variant
Dim q As Long

elLast = elemSet.Last
rc = elemSet.Reset

rc = femap.feAppStatusShow(True, elCount)

Do

elCurr = elemNum(q)
elCurr = elCurr

rc = femap.feMeasureMeshMassProp(-elCurr, 0, False, False, Length, area, volume, _
structMass, nonstructMass, totalMass, structCG, nonstructCG, totalCG, inertia, inertiaCG )

rc = el.Get(elCurr)

		If (el.topology = 7) Or (el.topology = 8) Then

			'For an 8-Node Solid, the characteristic Length Is (elements volume)/(Max Side area) As defined
			'in the LS-DYNA theory manual on pg. 22.1.  Although the thoery manual does not define how
			'to find the characteristic length of a 6-node wedge, it was found that if the wedge is extruded,
			'meaning that the front and back faces are the same, characteristic length can be assumed to be
			'2*(elements volume0/(max side area)
			'This is why the factor SF is employed

			If el.topology = 7 Then
				SF = 2
			ElseIf el.topology = 8 Then
				SF = 1
			End If


dAreaHolder = 0

For i = 1 To 6
	rc = el.GetFaceArea(i, dArea)
	If dArea > dAreaHolder Then
		dAreaHolder = dArea
	End If
Next i

'charLength is taken from LS-DYNA theory manual pg number 22.1

charLength = volume / dAreaHolder * SF

pID = el.propID
rc = proper.Get(pID)
matID = proper.matlID

timeStep = charLength/(0.06*wSpeed(matID)+((0.06*wSpeed(matID))^2+(wSpeed(matID))^2)^0.5)

		'This timestep calculation is taken from equation #22.2 of the LS-DYNA theory manual

If timeStep < globalMin Then
	globalMin = timeStep
	minelID = elCurr
End If

elemOutIDS(t) = elCurr
elemOutValues(t) = timeStep

End If

rc = femap.feAppStatusUpdate(t)

t = t + 1
q = q + 1

Loop Until elCurr = elemLast


'------------------------------------------------------------------------------
'Output Code



Dim osID As Variant
Dim ovID As Long


'    If osID = 0 Then
		'No Output Set, need to make one
		osID = feOutputSet.NextEmptyID()
        OutputNums(2) = osID
        feOutputSet.title = "Wave Speed"
        feOutputSet.program = 1
        feOutputSet.analysis = 0
        feOutputSet.value = 0#
        feOutputSet.Put (osID)
'    End If

            OutputNums(2) = osID

    ' Now get an Output Vector

    feOutput.ID = 299999

    ovID = feOutput.NextEmptyID()
    feOutput.ID = ovID

    rc = feOutput.InitScalarAtElem(osID, ovID, "Wave Speed", 0, True)

   veID = elementID
   veValue = eValues
   rc = feOutput.PutOutputList(elCount, elemOutIDS, elemOutValues)

   rc = feOutput.Put(300000)


FAIL:

End Sub

Sub OutputCombiner()

'In each of the three preceding routines, an output set is created.  This script combines all of these
'sets into one, and then deletes the preceding three.


	Dim femap As femap.model
	Set femap = GetObject(,"femap.model")

	Dim feOutputSet As Object
	Set feOutputSet = femap.feOutputSet

	Dim oSet As Object
	Set oSet = femap.feSet


Dim osID As Variant
Dim ovID As Long

Dim approach(3) As Long
Dim from_Vec(3) As Long
Dim to_setID(3) As Long
Dim to_Vec(3) As Long
Dim Groups(3) As Long
Dim factor(3) As Double
Dim overwrite(3) As Boolean
Dim ply_summary(3) As Boolean
Dim set_info(3) As Boolean


		osID = feOutputSet.NextEmptyID()
        feOutputSet.title = "Wave Speed"
        feOutputSet.program = 1
        feOutputSet.analysis = 0
        feOutputSet.value = 0#
        feOutputSet.Put (osID)


   For i = 0 To 2
	approach(i) = 1
	from_Vec(i) = 0
	to_setID(i) = osID
	to_Vec(i) = 0
	Groups(i) = 0
	factor(i) = 1
	overwrite(i) = True
	ply_summary(i) = False
	set_info(i) = False
Next i

	rc = femap.feOutputProcess( 3, True, 1, _
	approach, OutputNums, from_Vec, to_setID, to_Vec, Groups, factor, _
	overwrite, ply_summary, set_info )


        textOut = "The minimum calculated time step is " + Str$(globalMin)
        Call femap.feAppMessage(0, textOut)
        textOut = "At Element " + Str$(minelID)
        Call femap.feAppMessage(0, textOut)

femap.feAppMessage(0,"Cleaning Up Output")



For i = 0 To 2
rc = oSet.Add(OutputNums(i))
Next i

rc = femap.feDelete(28, oSet.ID)

rc = femap.feRenumber(28, -osID, OutputNums(0))

rc = femap.feAppMessage(0,"Program Completed")
rc = femap.feAppMessageBox(0,"Program Completed")


End Sub
